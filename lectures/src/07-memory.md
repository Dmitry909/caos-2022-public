## Обращения к памяти.
Сохранить значение регистра eax по адресу 0x40100,
а потом загрузить обратно в регистр ebx:
```x86asm
    mov %eax, 0x40100
    mov 0x40100, %ebx
```

Как правило, мы используем в качестве адресов метки:
```x86asm
    .global main
main:
    mov x, %eax
    call writei32
    call finish

x:  .int 43
```

Не любой адрес в памяти доступен для чтения и тем более записи:
```x86asm
    .global main
main:
    inc x
    call finish

x:  .int 43
```

## Секции .data и .bss
Код и данные записываются в одну из *секций* исполняемого
файла. По умолчанию это секция `.text`, в которой
ожидается машинный код и которая недоступна для записи.

Данные можно положить в секцию `.data`:
```x86asm
    inc x

    .data
x:  .int 43

    .text
    call finish
```

## Endianness

x86 — little endian:
```x86asm
    // 0x40100:  00 00 00 00  00 00 00 00
    movl $0xabcdef, 0x40100
    // 0x40100:  ef cd ab 00  00 00 00 00
```
Младший байт по младшему адресу в памяти.

## Расширение

```x86asm
    movzbl %al, %edi  // zero-extend, расширение нулями
    movsbl %al, %edi  // sign-extend, расширение знаковым битом
    cdq  // sign-extend eax to edx:eax
```

В синтаксисе Intel —
[movzx](https://www.felixcloutier.com/x86/movzx),
[movsx](https://www.felixcloutier.com/x86/movsx).

## Разные способы адресации в x86

```x86asm
    mov x + 4, %eax   // прямой
    mov $x, %esi
    mov 4(%esi), %eax // косвенный

    .bss
x:  .skip 4 * 100

x5: .int x + 4*5
```

Общий вид обращения к памяти:

OFFSET(BREG, IREG, SCALE)

Адрес вычисляется по формуле:

BREG + OFFSET + IREG * SCALE

## Стек
Writable область памяти, которая используется
как стек. Стек растёт *вниз*. На верхушку стека (первый занятый байт) указывает регистр esp.

```x86asm
    push %eax  // то же, что sub $4, %esp; mov %eax, (%esp)
    pop %eax   // то же, что mov (%esp), %eax; add $4, %esp
```

## Подпрограммы

Когда в ДЗ просят сдать «функцию» или «подпрограмму»:
1) исполнение начинается с метки с именем подпрограммы;
2) метка должна быть `.global`;
3) чтобы вернуть управление в проверяющую программу,
   используйте инструкцию `ret`;
4) если меняете значения регистров, кроме `eax`, `ecx` и `edx`,
   сохраняйте их в стек и потом восстанавливайте.

Например, если вас просят написать функцию `foobar`,
и вам нужно менять регистры `esi` и `edi`:
```x86asm
    .global foobar
foobar:
    push %esi
    push %edi
    ... // делаем что просят, можем портить esi и edi
    pop %edi // восстанавливаем в обратном порядке
    pop %esi
    ret
```
(Подробнее в следующей лекции.)

## Устройство ОЗУ
Мы уже видели SRAM — память на триггерах.

Основную часть памяти компьютера составляет DRAM:
![](./dram.png)

Процессор и память общаются посредством *шины* (bus):
![](./system_bus.png)
